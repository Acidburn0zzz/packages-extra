# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux39-r8168
_pkgname=r8168
pkgver=8.031.00
pkgrel=5
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux39')
makedepends=('linux39-headers')
provides=("$_pkgname=$pkgver")
groups=('linux39-extramodules')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2 linux-3.8-fix.patch)
install=$_pkgname.install
sha256sums=('5c617b3c08aca18d1eb24d33f77df40020eb64fb32c8e4008265e08b7ffe5779'
            '8fd87309d960394a4ad6d87488109009ea3d61e0cf534f6e6189c0152ed602fa')

_extramodules=extramodules-3.9-MANJARO
_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"

build() {
	cd "$_pkgname-$pkgver"

	patch -Np1 -i ../linux-3.8-fix.patch

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
