#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

_extramodules=extramodules-3.9-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

pkgname=linux39-catalyst
_pkgname=catalyst
pkgver=13.1
pkgrel=0.2
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('linux39' 'catalyst-utils')
makedepends=('gcc-libs' 'gcc>4.0.0' 'make' 'linux39-headers')
conflicts=('linux39-catalyst-legacy')
provides=("$_pkgname=$pkgver")
groups=('linux39-extramodules')
install=catalyst.install

source=(http://www2.ati.com/drivers/linux/amd-driver-installer-catalyst-${pkgver}-linux-x86.x86_64.zip
        ati_make.sh
        makefile_compat.patch
        arch-fglrx-3.7.patch
        kernel-3.8-fixes.patch)

sha256sums=('67898a922b6b58f25a276a144f16b19014f79c39e4d44d8d2883a467d31e34ad'
	 'e1d3cedbfc6a239d5d71458c458137f1a7404ebddfa838557d25c965dd49c845'
	 'd344df75ffbe9bd899e288862f41983c2e24b5eff3e7e8cf02d4891eeb137fff'
         '4a93aab0f829be4474a869fa43dd80e2d43348929e2236872a70040c3bbee4f5'
         '71d723cc8348638aeab3f27c1fcbd84e8a2fd106d22778237625e90786f221f2'
)

build() {

    msg "Setting compilation variables ..."
  ## include ati_make.sh and use _ati_check function from it
    . ati_make.sh
    _ati_check


    msg "Unpacking archive ..."
    sh ./amd-driver-installer*.run --extract fglrx-install


    if [ "${CARCH}" = "x86_64" ]; then
	BUILDARCH=x86_64
	_archdir=x86_64
    fi
    if [ "${CARCH}" = "i686" ]; then
	BUILDARCH=i386
	_archdir=x86
    fi

    cd "${srcdir}/fglrx-install/"


    msg "Applying patch(es) ..."
   ## makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
   ## works only in combination with ati_make.sh script
    patch -Np1 -i ../makefile_compat.patch

    patch -Np1 -i ../arch-fglrx-3.7.patch
    patch -Np1 -i ../kernel-3.8-fixes.patch

    cd "${srcdir}/fglrx-install/common/lib/modules/fglrx/build_mod"
    cp "${srcdir}/fglrx-install/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" .
    cp 2.6.x/Makefile .


    msg "Compiling fglrx module ..."
   ## Compile module using _ati_check variables
    make -C /usr/lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
      MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
      PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}

package() {
    cd "${srcdir}/fglrx-install/common/lib/modules/fglrx/build_mod"
    install -m755 -d "${pkgdir}/usr/lib/modules/${_extramodules}/"
    install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"
    find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/" $startdir/*.install

    ## License
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 "${srcdir}/fglrx-install/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
