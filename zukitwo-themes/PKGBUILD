# Based on the file created for Arch Linux by:
# grimi <grimi at poczta dot fm>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=zukitwo-themes
pkgver=20120817
pkgrel=2
pkgdesc="Themes for gtk3, gtk2, metacity, xfwm4, gnome-shell and emerald..."
arch=('any')
url="http://gnome-look.org/content/show.php/Zukitwo?content=140562"
license=('GPL')
depends=('gtk-engines' 'gtk-engine-murrine' 'gtk-engine-unico>=1.0.2')
source=("${pkgname}-${pkgver}.zip::http://gnome-look.org/CONTENT/content-files/140562-Zukitwo.zip")
sha256sums=('292f32a50221064b99ab5e7ca0076493e343c30e135a84e46be2034c0e8eee47')

package() {
  # fix xfce panel
  sed '/Xfce/s/\(theme-panel\)/\1-light/' -i Zukitwo{,-Dark}/gtk-2.0/widgets/panel.rc

  # install themes
  find Zukitwo*/ -type f \
      -exec install -Dm644 "{}" "$pkgdir/usr/share/themes/{}" \;

  # emerald part
  install -d zuki "$pkgdir/usr/share/emerald/themes/Zukitwo"
  tar xf Zukitwo.emerald -C zuki
  install -m644 zuki/*.{ini,png} "$pkgdir/usr/share/emerald/themes/Zukitwo"
}

# vim:set ts=2 sw=2 et:
