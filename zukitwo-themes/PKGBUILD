# Based on the file created for Arch Linux by:
# grimi <grimi at poczta dot fm>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=zukitwo-themes
pkgver=20120708
pkgrel=1
pkgdesc="Themes for gtk3, gtk2, metacity, xfwm4, gnome-shell and emerald..."
arch=('any')
url="http://gnome-look.org/content/show.php/Zukitwo?content=140562"
license=('GPL')
depends=('gtk-engines' 'gtk-engine-murrine' 'gtk-engine-unico>=1.0.2')
source=("Zukitwo-$pkgver.zip::http://gnome-look.org/CONTENT/content-files/140562-Zukitwo.zip"
        "xfce-panel.patch")
sha256sums=('76df9ed0df989713ee5998299f19e7b644ed78182dc17829f9f4f4c1a1a571b8'
            '4067090b0f9d97ed659af7f05c329f4bf83b0474aff03493948f1f9d0b4056f4')

package() {
  # patches for xfce
  patch -Np0 -i xfce-panel.patch

  # fix font definitions
  sed 's/font:/font-size:/g' -i Zukitwo-Shell/gnome-shell/gnome-shell.css

  # install themes
  find Zukitwo*/ -not -name *Thumbs.db -type f \
      -exec install -Dm644 "{}" "$pkgdir/usr/share/themes/{}" \;

  # emerald part
  install -d zuki "$pkgdir/usr/share/emerald/themes/Zukitwo"
  tar xf Zukitwo.emerald -C zuki
  install -m644 zuki/*.{ini,png} "$pkgdir/usr/share/emerald/themes/Zukitwo"
}

# vim:set ts=2 sw=2 et:
