# Based on the file created for Arch Linux by:
# Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# M0Rf30 morf3089 at gmail dot com

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=lightdm-unity-greeter
pkgver=13.04.1
pkgrel=1
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
depends=('gnome-doc-utils' 'lightdm' 'gnome-common' 'libindicator3' 'consolekit-git' 'indicator-session' 'adwaita-manjaro-themes' 'faenza-green-icon-theme' 'manjaro-artwork')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala>=0.16.0' 'autoconf')
options=('!libtool')
install=unity-greeter.install
source=(#"https://launchpad.net/unity-greeter/0.2/$pkgver/+download/unity-greeter-$pkgver.tar.gz"
    "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}.orig.tar.gz"
		manjaro-theming.patch
		translation.patch
		cof.png
		logo.png
		manjaro_badge.png
		xfce_badge.png)
sha256sums=('f9059c96f210e02c079d81b4b653a14ecc44996e633b36e754b747acc93a7d90'
            'e38c2cbe5859e8d31b59ce0540f5d833230c74e2f6431d8ece7d33f274628dfb'
            'd518ee58fdc37a191ace6684a55f026af76ff2c7b48529d662176ae9064e8ca7'
            '5c22846fff201ae87bbc737dd3f3099b3bb41e5812667f294cadd1dfe19cd7b0'
            'd0326318b3b787a1f2f359a31cb1e8c957e4487f9896fd144861e7fe1ffb5efb'
            '7ac0effe19b542a267e6c5bb4ae09f228817c68875f9d26f3644729179261cc9'
            '4832c1a09dda9e888400433edf7156204ef85e74aedddd0304ef62f5377c44c3')
            
build() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  # Apply Manjaro theming patch
  patch -Np1 -i "${srcdir}/manjaro-theming.patch"
  
  # Apply translation patch
  patch -Np1 -i "${srcdir}/translation.patch"

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  # fix autoconf
  sed -i 's:AM_CONFIG_HEADER(config.h):AC_CONFIG_HEADERS(config.h):g' configure.ac

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Use Manjaro logos 
  cp $srcdir/{cof,logo,manjaro_badge,xfce_badge}.png $pkgdir/usr/share/unity-greeter

}

# vim:set ts=2 sw=2 et:
