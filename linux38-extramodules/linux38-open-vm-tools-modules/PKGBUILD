# Based on the file created for Arch Linux by:
# Sergej Pupykin <pupykin.s+arch@gmail.com>
# Krzysztof Raczkowski <raczkow@gmail.com>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux38-open-vm-tools-modules
_pkgname=open-vm-tools-modules
groups=('linux38-extramodules')
epoch=1
pkgver=9.2.3
_pkgsubver=1031360
pkgrel=3
pkgdesc="kernel modules for the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'procps' 'icu' 'uriparser' 'linux38-headers')
depends=("linux38")
provides=("$_pkgname=$pkgver")
groups=('linux38-extramodules')
install=$_pkgname.install
options=('!strip')
source=("http://downloads.sourceforge.net/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz"
	"modprobe.conf")
sha256sums=('1a004ea1675101fd44cddda299e2e9ac254388769b69f41b7ff5d1797549c8f1'
            '27b83062b8b6a279e8bfb13f10566467377f38ac077e75933211912cac4a085d')

build() {
  _extramodules=extramodules-3.8-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  sed -i 's|proc-3.2.8|procps|g' configure
  sed -i 's|putname(name);|__putname(name);|' modules/linux/vmblock/linux/control.c

  [ $NOEXTRACT -eq 1 ] || ./configure \
      --prefix=/usr \
      --without-x \
      --with-linuxdir=/usr/lib/modules/$_kernver

  make -C modules modules
}

package() {
  _extramodules=extramodules-3.8-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  depends=("linux38")

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  mkdir -p $pkgdir/usr/lib/modules/${_extramodules}/
  for MOD in `find -type f -name '*.ko'`; do
    install -Dm644 $MOD $pkgdir/usr/lib/modules/${_extramodules}/
  done
  gzip $pkgdir/usr/lib/modules/${_extramodules}/*.ko

  install -D -m 644 ${srcdir}/modprobe.conf ${pkgdir}/etc/modprobe.d/${pkgname}.conf
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "${startdir}/${_pkgname}.install"
}
