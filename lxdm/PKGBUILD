# $Id: PKGBUILD 74703 2012-08-02 08:45:29Z bpiotrowski $
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: kiefer <jorgelmadrid@gmail.com>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=lxdm
pkgver=0.4.1
pkgrel=17
pkgdesc='Lightweight X11 Display Manager'
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/lxdm/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'xorg-server' 'consolekit' 'shiki-nouveau-theme' 'lxdm-manjaro-theme')
makedepends=('intltool')
install=${pkgname}.install
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm' 'etc/lxdm/Xsession'
        'etc/lxdm/PreLogin' 'etc/lxdm/LoginReady' 'etc/lxdm/PostLogin'
        'etc/lxdm/PostLogout' 'etc/lxdm/PreReboot' 'etc/lxdm/PreShutdown')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz
        glib2-2.32.0.patch lxdm.patch lxdm.conf.patch Xsession.patch
        greeter-session.patch pam-env-vars.patch lxdm-pam service lxdm.login_pane.patch lxdm.xserverrestart.patch)


build() {
    cd $srcdir/$pkgname-$pkgver

    patch -Np1 -i $srcdir/glib2-2.32.0.patch
    patch -Np1 -i $srcdir/greeter-session.patch
    patch -Np1 -i $srcdir/pam-env-vars.patch
	patch -Np0 -i $srcdir/lxdm.login_pane.patch
	patch -Np0 -i $srcdir/lxdm.xserverrestart.patch

    ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib/lxdm
    make
  	
    patch -Np0 -i $srcdir/lxdm.patch
    patch -Np0 -i $srcdir/lxdm.conf.patch
    patch -Np0 -i $srcdir/Xsession.patch
}


package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install

    install -m644 $srcdir/lxdm-pam $pkgdir/etc/pam.d/lxdm
    install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/lxdm.service
    install -d $pkgdir/var/{lib,run}/lxdm

    # fix the greeter location
    sed -i -e 's/local\/libexec/lib\/lxdm/' $pkgdir/etc/lxdm/lxdm.conf

    # avoid conflict with filesystem>=2012.06
    rm -r $pkgdir/var/run

	# Fix logout behavior
	echo '#!/bin/sh' > "${pkgdir}/etc/lxdm/PostLogout"
	echo '' >> "${pkgdir}/etc/lxdm/PostLogout"
	echo '# Sets the desktop background to solid black. Useful if you have multiple monitors.' >> "${pkgdir}/etc/lxdm/PostLogout"
	echo 'xsetroot -solid black' >> "${pkgdir}/etc/lxdm/PostLogout"
}


sha256sums=('9e0d0a5672fcf31a18de8178ce73eab1723d6ae7097dfe41e9fe2c46e180cf08'
            'c6a766502f18b6e073aa021d680bbb351791bf2646f6bdf6c9093ad2a83ddcbc'
            'c4dbea49b6b4cab407d621e868e1bda0111df1012a244abde36cac54ea4a5735'
            '760fe02e45176ef3a1e676af576104600483f0e358a45ef637b296fdade5d3c3'
            'b901a2141f3becea005952f59d54c13357c0ba12e9ebbde24cf395a50ce748e2'
            '029c77c53c42188ce9fc3372c77aac942517e449d77be13717bb79108f9d4971'
            'dd18af5704a9b52d0730508a7dae8604ae4e62c12a04dc386138fa082a5faff9'
            'c423180ca9f140600435ac05bf11c16e1aac6baf762145a2ec80812651ee439f'
            '504521b685ee2a9d6d6a4850c8bf3c8f4c911a8f986e1d6641316cfc1284774e'
            '8f2112373bd0cca9509c0cfade7543dc875f5340bf9e796bc06a6d7e41149cf5'
            '84f8062b7a1f8065adf86d49bab84520532f9dd9ecbfcc82856ac84465c9eea9')
