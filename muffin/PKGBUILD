# Based on the file created for Arch Linux by:
# Ner0
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=muffin
pkgver=1.1.1
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'libxxf86vm' 'startup-notification' 'zenity' 'dconf')
makedepends=('libltdl' 'intltool' 'gnome-doc-utils' 'pkg-config')
provides=('muffin-wm')
replaces=('muffin-wm')
conflicts=('muffin-git' 'muffin-wm')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/tarball/$pkgver")
sha256sums=('fc69e23faf08549d87b974d253bae350d16b5d96eeea07e95e7cb602b1d7f75c')

build() {
  cd linuxmint-*

  autoreconf -vfi
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin --localstatedir=/var --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd linuxmint-*
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
