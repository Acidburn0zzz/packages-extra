# Based on the file created for Arch Linux by:
# Maintainer : Piotr Gorski <sir_lucjan@bridgelinux.pl>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=xfdesktop
_pkgname=xfdesktop
_git=37baff27944ba6282e420069e0993f08953ff482
pkgver=4.11.1
pkgrel=5
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=("libxfce4ui>=4.11" 'thunar' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool' 'xfce4-dev-tools')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")
options=('!libtool')
install=xfdesktop.install
source=(#http://archive.xfce.org/src/xfce/$_pkgname/4.11/$_pkgname-$pkgver.tar.bz2
        $pkgname-$pkgver-$pkgrel.tar.bz2::http://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_git.tar.bz2)
sha256sums=('a519ca59756afc84168bd514a768f2c2fb3bb12846f8aa798cd9af1c35e3e4bf')

build() {
  cd "$srcdir/$_pkgname-$_git"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-thunarx \
    --disable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_git"
  make DESTDIR="$pkgdir" install
}
