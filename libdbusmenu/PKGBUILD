# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>

pkgbase=libdbusmenu
pkgname=('libdbusmenu' 'libdbusmenu-gtk3')
pkgver=0.6.1
pkgrel=1
pkgdesc="Small little library that passes a menu structure across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
makedepends=('gtk2' 'gtk3' 'intltool' 'gnome-doc-utils' 'gobject-introspection' 'vala' 'python2')
options=('!libtool' '!emptydirs')
source=(http://launchpad.net/dbusmenu/0.6/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('820b6999dd1008328bfa442575d859a1')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-scrollkeeper --disable-tests
  make
}

package_libdbusmenu() {
  depends=('glib2')
 optdepends=('python2: for dbusmenu-dumper tool'
             'gtk2: for dbusmenu-bench tool')
  cd "$srcdir/$pkgbase-$pkgver"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -C tools DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}


package_libdbusmenu-gtk3() {
  pkgdesc="Small little library that passes a menu structure across DBus (GTK+ 3 library)"
  depends=('gtk3' 'libdbusmenu')
  cd "$srcdir/$pkgbase-$pkgver"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -j1 -C libdbusmenu-gtk DESTDIR="$pkgdir/" install
  make -C libdbusmenu-glib DESTDIR="$pkgdir/" uninstall
}
