# Contributor: OK100 <ok100 at lavabit dot com>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=compton-git
_gitname="compton"
pkgver=a053c0a
pkgrel=1
pkgdesc="X Compositor (a fork of xcompmgr-dana)"
arch=(i686 x86_64)
url="https://github.com/chjj/compton"
license=('MIT')
depends=('libgl' 'dbus' 'libxcomposite' 'libxdamage' 'libxrandr' 'pcre' 'libconfig')
makedepends=('git' 'asciidoc')
source=("git://github.com/chjj/compton.git")
md5sums=("SKIP")

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr
  make docs
}

package() {
  cd "$srcdir/$_gitname"

  make PREFIX="$pkgdir/usr" install

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  # example conf
  install -D -m644 "compton.sample.conf" "$pkgdir/etc/xdg/compton.conf.example"
}
