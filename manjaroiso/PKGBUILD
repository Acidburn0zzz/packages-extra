#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=manjaroiso
pkgname=('manjaroiso' 'manjaroiso-profiles' 'manjaroiso-community-profiles')
pkgver=0.8.7
pkgrel=0.2
arch=('any')
url="https://gitorious.org/chakra/manjaroiso"
license=('GPL')
makedepends=('git')
_git=yes

getsource() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $1 ] ; then
    if [ "$_git" == "yes" ] ; then
       cd $1 && git pull origin master
    else
       cd $1 && git pull origin master && git checkout -b $pkgver $pkgver
    fi
    msg "The local files are updated."
  else
    git clone $2 $1
    if [ "$_git" != "yes" ] ; then
       cd $1 && git checkout -b $pkgver $pkgver
    fi
  fi

  msg "GIT checkout done or server timeout"
}

build() {
  cd "$srcdir"
  getsource "manjaroiso" "git://git.manjaro.org/core/manjaroiso.git"
}

package_manjaroiso() {
  pkgdesc="Manjaro Linux livecd/liveusb generation scripts"
  depends=('mkinitcpio' 'mkinitcpio-nfs-utils' 'nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'haveged' 'pacman>=4.1')
  optdepends=('qemu: quickly test isos' 'manjaroiso-profiles: Manjaro Linux ISO-Image profiles')
  cd ${srcdir}/${pkgbase}/src
  make DESTDIR="$pkgdir" install
  cd ${srcdir}/${pkgbase}/
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  cp -r configs/shared $pkgdir/usr/share/manjaroiso/configs/shared
}

package_manjaroiso-profiles() {
  pkgdesc="Manjaroiso Profiles"
  depends=('manjaroiso')
  cd ${srcdir}/${pkgbase}/
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  for conf in build cinnamon net openbox xfce
  do
      cp -r configs/${conf} $pkgdir/usr/share/manjaroiso/configs/${conf}
  done
}

package_manjaroiso-community-profiles() {
  pkgdesc="Manjaroiso Community Profiles"
  depends=('manjaroiso')
  cd ${srcdir}/${pkgbase}/
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  for conf in e17 kde lxde mate
  do
      cp -r configs/${conf} $pkgdir/usr/share/manjaroiso/configs/${conf}
  done
}
