#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=manjaroiso
pkgname=('manjaroiso' 'manjaroiso-profiles') # 'manjaroiso-community-profiles')
pkgver=0.8.9
pkgrel=0.1
arch=('any')
url="http://git.manjaro.org/core/manjaroiso"
license=('GPL')
makedepends=('git')
_git=snapshot
_gitver=702f11d25ed9ce5d34c03cbac1eb0cbaafe28178

if [ "$_git" == "snapshot" ]; then
   source=("manjaroiso-$pkgver-$pkgrel.tar.gz::http://git.manjaro.org/core/manjaroiso/repository/archive.tar.gz?ref=$_gitver")
   sha256sums=('SKIP')
fi

if [ "$_git" == "Yes" ]; then
   source=(git+http://git.manjaro.org/core/manjaroiso.git)
   sha256sums=('SKIP')
fi

if [ "$_git" == "No" ]; then
   source=("manjaroiso-$pkgver.tar.gz::http://git.manjaro.org/core/manjaroiso/repository/archive.tar.gz?ref=$pkgver")
   sha256sums=('663802052cf7b77be549e427ef8fe7780bb88ace7b359ece72a94001ff572bbc')
fi

package_manjaroiso() {
  pkgdesc="Manjaro Linux livecd/liveusb generation scripts"
  depends=('devtools' 'mkinitcpio' 'mkinitcpio-nfs-utils' 'nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'haveged' 'arch-install-scripts' 'pacman>=4.1')
  optdepends=('qemu: quickly test isos' 'manjaroiso-profiles: Manjaro Linux ISO-Image profiles')
  if [ -e "$srcdir/$pkgbase" ]; then
     cd "$srcdir/$pkgbase/src"
  fi
  if [ -e "$srcdir/$pkgbase.git" ]; then
	   cd "$srcdir/$pkgbase.git/src"
  fi
  make DESTDIR="$pkgdir" install
  if [ -e "$srcdir/$pkgbase" ]; then
     cd "$srcdir/$pkgbase"
  fi
  if [ -e "$srcdir/$pkgbase.git" ]; then
	   cd "$srcdir/$pkgbase.git"
  fi
  sed -i "s|VERSION|${pkgver}-${pkgrel}|g" $pkgdir/usr/share/manjaroiso/functions/messages
}

package_manjaroiso-profiles() {
  pkgdesc="Manjaroiso Profiles"
  depends=('manjaroiso')
  if [ -e "$srcdir/$pkgbase" ]; then
     cd "$srcdir/$pkgbase"
  fi
  if [ -e "$srcdir/$pkgbase.git" ]; then
	   cd "$srcdir/$pkgbase.git"
  fi
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  for conf in build kde net openbox shared xfce
  do
      cp -r configs/${conf} $pkgdir/usr/share/manjaroiso/configs/${conf}
  done
}

package_manjaroiso-community-profiles() {
  pkgdesc="Manjaroiso Community Profiles"
  depends=('manjaroiso-profiles')
  if [ -e "$srcdir/$pkgbase" ]; then
     cd "$srcdir/$pkgbase"
  fi
  if [ -e "$srcdir/$pkgbase.git" ]; then
	   cd "$srcdir/$pkgbase.git"
  fi
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  for conf in cinnamon enlightenment lxde mate openbox-legacy
  do
      cp -r configs/${conf} $pkgdir/usr/share/manjaroiso/configs/${conf}
  done
}
