# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac
pkgver=0.9.8rc1
pkgrel=1
pkgdesc="A gtk3 frontend for pyalpm"
arch=(any)
url="https://git.manjaro.org/core/pamac"
license=('GPL')
depends=('python>=3.3' 'python<3.4' 'pacman>=4.1' 'pyalpm=0.6.2.2' 'gtk3>=3.6' 'python-requests' 'python-gobject' 'python-dbus' 'dbus-glib' 'libnotify' 'namcap')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome' 'lxpolkit: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'git')
backup=('etc/pamac.conf')
replaces=('pamac-aur')
options=(!emptydirs)
install=$pkgname-install
_git=Yes
_snapshot=7b04b921448de9d4841c0dffd36e0195a96e3e1f

if [ "$_git" == "Yes" ]; then
   source=(git+http://git.manjaro.org/core/pamac.git)
   sha256sums=('SKIP')
else
   #source=("pamac-$pkgver.tar.gz::http://git.manjaro.org/core/pamac/repository/archive.tar.gz?ref=v$pkgver")
   #sha256sums=('89fa495cf73c12d6b9506fc40bffdc6a0afcff70ae900181b67d5f926b97550c')
   source=("pamac-$pkgver-$pkgrel.tar.gz::http://git.manjaro.org/core/pamac/repository/archive.tar.gz?ref=$_snapshot"
           "translation1.patch::http://git.manjaro.org/core/pamac/commit/903df69fd43266dd8ea4b08615f70c92782d6138.patch"
           "translation2.patch::http://git.manjaro.org/core/pamac/commit/2a0479080ff269d6da91ae3719cc88a098bac13c.patch")
   sha256sums=('97023b56fee7b4f6d0f28af24a7401572624c32912f0a7ddd024ed7b634b6cee' 'SKIP' 'SKIP')
fi

package() {
  if [ -e "$srcdir/core-$pkgname" ]; then
	   cd "$srcdir/core-$pkgname"
  fi
  if [ -e "$srcdir/$pkgname" ]; then
     cd "$srcdir/$pkgname"
  fi
  if [ -e "$srcdir/$pkgname.git" ]; then
	   cd "$srcdir/$pkgname.git"
  fi

	# match version with pkgver
	sed -i -e "s|^.*version =.*|version = \'$pkgver\'|" pamac-manager.py

	#patch -Np1 -i "${srcdir}/translation1.patch"
	#patch -Np1 -i "${srcdir}/translation2.patch"

	python setup.py install --root="$pkgdir/" --optimize=1
	mkdir -p "${pkgdir}/usr/share/pamac"
	cp -r "gui" "${pkgdir}/usr/share/pamac"
	mkdir -p "${pkgdir}/usr/share/icons/hicolor"
	cp -r "data/icons/"* "${pkgdir}/usr/share/icons/hicolor" 
	install -Dm744 "pamac-daemon.py" "${pkgdir}/usr/bin/pamac-daemon"
	install -Dm755 "pamac-manager.py" "${pkgdir}/usr/bin/pamac-manager"
	install -Dm755 "pamac-updater.py" "${pkgdir}/usr/bin/pamac-updater"
	install -Dm755 "pamac-tray.py" "${pkgdir}/usr/bin/pamac-tray"
	install -Dm755 "pamac-refresh.py" "${pkgdir}/usr/bin/pamac-refresh"
	install -Dm755 "pamac-install.py" "${pkgdir}/usr/bin/pamac-install"
	install -Dm755 "pamac-check_updates.py" "${pkgdir}/usr/bin/pamac-check_updates"
	install -Dm644 "data/applications/pamac-tray.desktop" "${pkgdir}/etc/xdg/autostart/pamac-tray.desktop"
	install -Dm644 "data/applications/pamac-manager.desktop" "${pkgdir}/usr/share/applications/pamac-manager.desktop"
	install -Dm644 "data/applications/pamac-updater.desktop" "${pkgdir}/usr/share/applications/pamac-updater.desktop"
	install -Dm644 "data/config/pamac.conf" "${pkgdir}/etc/pamac.conf"
	install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
	install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
	install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
	install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
	# create .mo translation files
	mkdir -p mo
	for i in `ls po | sed s'|.po||'` ; do
		msgfmt -c "po/$i.po" -o "mo/$i.mo"
	done
	# add translation in policy file
	itstool -j "data/polkit/org.manjaro.pamac.policy" -o "data/polkit/org.manjaro.pamac.policy.out" `ls mo/*`
	# rename mo files only here else istool failed
	for i in `ls mo | sed s'|.mo||'` ; do
		mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
		mv "mo/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/pamac.mo"
	done
	install -Dm644 "data/polkit/org.manjaro.pamac.policy.out" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"

}

# vim:set ts=2 sw=2 et:
