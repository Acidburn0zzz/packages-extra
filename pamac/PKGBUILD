# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac
pkgver=0.4.5
pkgrel=1
pkgdesc="A gtk3 frontend for pyalpm"
arch=(any)
url="https://git.manjaro.org/core/pamac"
license=('GPL')
depends=('python' 'pyalpm' 'gtk3' 'python-gobject' 'python-dbus' 'dbus-glib' 'polkit-gnome' 'libnotify')
makedepends=()
options=(!emptydirs)
install=
source=("pamac-$pkgver.tar.gz::http://git.manjaro.org/core/pamac/archive-tarball/v$pkgver")
sha256sums=('e45d8d9cc32bec1ceeb035bc51061601ea5e00ec437a689b7e4a8fe0af35877c')

package() {
	cd "$srcdir/core-$pkgname"
	python setup.py install --root="$pkgdir/" --optimize=1
	mkdir -p "${pkgdir}/usr/share/pamac"
	cp -r "gui" "${pkgdir}/usr/share/pamac"
	cp -r "data/icons" "${pkgdir}/usr/share/pamac"
	cp -r "data/applications" "${pkgdir}/usr/share/"
	install -Dm744 "pamac-daemon" "${pkgdir}/usr/bin/pamac-daemon"
	install -Dm755 "pamac-manager" "${pkgdir}/usr/bin/pamac-manager"
	install -Dm755 "pamac-updater" "${pkgdir}/usr/bin/pamac-updater"
	install -Dm755 "pamac-tray" "${pkgdir}/usr/bin/pamac-tray"
	install -Dm744 "pamac-refresh" "${pkgdir}/usr/bin/pamac-refresh"
	install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
	install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
	install -Dm644 "data/polkit/org.manjaro.pamac.policy" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"
	install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
	install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
}

# vim:set ts=2 sw=2 et:
