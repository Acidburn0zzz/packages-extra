# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac
pkgver=0.8.2
pkgrel=3
pkgdesc="A gtk3 frontend for pyalpm"
arch=(any)
url="https://git.manjaro.org/core/pamac"
license=('GPL')
depends=('python>=3.3' 'python<3.4' 'pacman>=4.1' 'pyalpm>=0.6.2.1' 'gtk3' 'python-gobject' 'python-dbus' 'dbus-glib' 'libnotify')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome' 'lxpolkit: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'git')
options=(!emptydirs)
install=$pkgname-install
_git=No

if [ "$_git" == "Yes" ]; then
   source=(git+http://git.manjaro.org/core/pamac.git)
   sha256sums=('SKIP')
else
   source=("pamac-$pkgver.tar.gz::http://git.manjaro.org/core/pamac/repository/archive?ref=v$pkgver"
           "pamac-$pkgver-url.patch::http://git.manjaro.org/core/pamac/commit/447f39e96697a65d1eadbe992a03571cd7afccdc.patch")
   sha256sums=('c6268601fd106119cca2222e8252b1c12c702a045d05b74ef4398d593497e060'
               '503530b1c0ecf4f03c74f94e1c080d4afd7c81531e1dc52ea106ff57fccad0bc')
fi

package() {
  if [ -e "$srcdir/core-$pkgname" ]; then
	   cd "$srcdir/core-$pkgname"
  else
     cd "$srcdir/$pkgname"
     patch -Np1 -i "$srcdir/pamac-$pkgver-url.patch"
     sed -i 's|from os.path import abspath|import os, sys|g' pamac-install.py
     sed -i 's|abspath(pkgstr)|os.path.realpath(pkgstr)|g' pamac-install.py
  fi
	python setup.py install --root="$pkgdir/" --optimize=1
	mkdir -p "${pkgdir}/usr/share/pamac"
	cp -r "gui" "${pkgdir}/usr/share/pamac"
	cp -r "data/icons" "${pkgdir}/usr/share/pamac"
	cp -r "data/applications" "${pkgdir}/usr/share/"
	cp -r "data/etc" "${pkgdir}/"
	install -Dm755 "pamac-manager.py" "${pkgdir}/usr/bin/pamac-manager"
	install -Dm755 "pamac-updater.py" "${pkgdir}/usr/bin/pamac-updater"
	install -Dm755 "pamac-tray.py" "${pkgdir}/usr/bin/pamac-tray"
	install -Dm755 "pamac-refresh.py" "${pkgdir}/usr/bin/pamac-refresh"
	install -Dm755 "pamac-install.py" "${pkgdir}/usr/bin/pamac-install"
	install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
	# create .mo translation files
	mkdir -p mo
	for i in `ls po | sed s'|.po||'` ; do
		msgfmt -c "po/$i.po" -o "mo/$i.mo"
	done
	# add translation in policy file
	itstool -j "data/polkit/org.manjaro.pamac.policy" -o "data/polkit/org.manjaro.pamac.policy.out" `ls mo/*`
	# rename mo files only here else istool failed
	for i in `ls mo | sed s'|.mo||'` ; do
		mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
		mv "mo/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/pamac.mo"
	done
	install -Dm644 "data/polkit/org.manjaro.pamac.policy.out" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"

}

# vim:set ts=2 sw=2 et:
