# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac
#_pkgver=0.7.5
#pkgver=$_pkgver.$(date +%m%d)
pkgver=0.7.5
pkgrel=1
pkgdesc="A gtk3 frontend for pyalpm"
arch=(any)
url="https://git.manjaro.org/core/pamac"
license=('GPL')
depends=('python>=3.3' 'python<3.4' 'pacman>=4.1' 'pyalpm>=0.6' 'gtk3' 'python-gobject' 'python-dbus' 'dbus-glib' 'polkit-gnome' 'libnotify')
makedepends=('gettext' 'itstool')
options=(!emptydirs)
install=$pkgname-install
source=("pamac-$pkgver.tar.gz::http://git.manjaro.org/core/pamac/archive-tarball/v$pkgver")
sha256sums=('9483be4b207d9495639977dec2405ec6a1c81671a830adc0b64b14a4e1e91b4b')

_git=no

getsource() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $1 ] ; then
    if [ "$_git" == "yes" ] ; then
       cd $1 && git pull origin master
    else
       cd $1 && git pull origin master && git checkout -b $pkgver $pkgver
    fi
    msg "The local files are updated."
  else
    git clone $2 $1
    if [ "$_git" != "yes" ] ; then
       cd $1 && git checkout -b $pkgver $pkgver
    fi
  fi

  msg "GIT checkout done or server timeout"
}

build() {
  cd "$srcdir"
  #getsource "core-$pkgname" "git://git.manjaro.org/core/$pkgname.git"
}

package() {
	cd "$srcdir/core-$pkgname"
	python setup.py install --root="$pkgdir/" --optimize=1
	mkdir -p "${pkgdir}/usr/share/pamac"
	cp -r "gui" "${pkgdir}/usr/share/pamac"
	cp -r "data/icons" "${pkgdir}/usr/share/pamac"
	cp -r "data/applications" "${pkgdir}/usr/share/"
	install -Dm744 "pamac-daemon.py" "${pkgdir}/usr/bin/pamac-daemon"
	install -Dm755 "pamac-manager.py" "${pkgdir}/usr/bin/pamac-manager"
	install -Dm755 "pamac-updater.py" "${pkgdir}/usr/bin/pamac-updater"
	install -Dm755 "pamac-tray.py" "${pkgdir}/usr/bin/pamac-tray"
	install -Dm755 "pamac-refresh.py" "${pkgdir}/usr/bin/pamac-refresh"
	install -Dm755 "pamac-install.py" "${pkgdir}/usr/bin/pamac-install"
	install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
	install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
	install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
	install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
	# create .mo translation files
	mkdir -p mo
	for i in `ls po | sed s'|.po||'` ; do
		msgfmt -c "po/$i.po" -o "mo/$i.mo"
	done
	# add translation in policy file
	itstool -j "data/polkit/org.manjaro.pamac.policy" -o "data/polkit/org.manjaro.pamac.policy.out" `ls mo/*`
	# rename mo files only here else istool failed
	for i in `ls mo | sed s'|.mo||'` ; do
		mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
		mv "mo/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/pamac.mo"
	done
	install -Dm644 "data/polkit/org.manjaro.pamac.policy.out" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"

}

# vim:set ts=2 sw=2 et:
