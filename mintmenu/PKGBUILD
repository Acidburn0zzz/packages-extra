# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Contributor: ryooichi <ryooichi+arch AT gmail DOT com>
# Real thanks goes to jelly, mike_93 and the original creators of mintmenu, usp, slab...
# See also:
#   http://bbs.archlinux.org/viewtopic.php?id=66987
#   http://bbs.archlinux.org/viewtopic.php?id=68633
#   http://github.com/jelly/archmenu

pkgname=mintmenu
pkgver=5.4.1
pkgrel=5
pkgdesc="Linux Mint Menu for MATE"
arch=('i686' 'x86_64')
url="http://packages.linuxmint.com/pool/main/m/mintmenu"
license=('GPL')
depends=('pygtk' 'python2-gobject' 'python2-gobject2' 'gksu' 'python2-xdg' 'xdg-utils' 'python2-keybinder2' 'python-xlib' 'glib2')
optdepends=('mate-menu-editor: for editing the gnome menu'
            'mint-translations: translations files'
            'gnome-packagekit: package manager button'
            'mate-screensaver: lock screen button')
source=("${url}/${pkgname}_${pkgver}.tar.gz"
		'removescript'
		'manjaro.patch'
		'logo5-16.svg'
		'logo5-32.svg')
install=$pkgname.install

package() {
  cd "$srcdir/${pkgname}"
  patch -uNp1 -r- -i "$srcdir/manjaro.patch"
  rm -f usr/lib/linuxmint/mintMenu/*.pyc usr/lib/linuxmint/mintMenu/plugins/*pyc
  sed -i -e "s/__version__/$pkgver/" usr/lib/linuxmint/mintMenu/mintMenu.py
  cp -R usr "$pkgdir/"
  install -m644 "$srcdir/logo5-16.svg" "$pkgdir/usr/lib/linuxmint/mintMenu/manjaro-icon-16.svg"
  install -m644 "$srcdir/logo5-32.svg" "$pkgdir/usr/lib/linuxmint/mintMenu/manjaro-icon-32.svg"
  install -m755 "$srcdir/removescript" "$pkgdir/usr/lib/linuxmint/mintMenu/"
  sed -i 's:#!/usr/bin/python:&2:' ${pkgdir}/usr/bin/mintmenu ${pkgdir}/usr/lib/linuxmint/mintMenu/{./,plugins/}*py
  sed -i 's:#!/usr/bin/env python:&2:' ${pkgdir}/usr/bin/mintmenu ${pkgdir}/usr/lib/linuxmint/mintMenu/{./,plugins/}*py
  cd "$pkgdir"/usr/lib/linuxmint/mintMenu
  ./compile.py
}

sha256sums=('e682770ab450d450471e8ceb5ab96136d89aa365bf8ea8095b175f9b63e5148c'
            '9533d0f5416af1f2b8ef5097f06e39135278eef856994d859c4ee7ba57d6fcaa'
            '2890bfbda16ace4c2f5bdbd0b20510606f49049bdfd51d66a70910b35425510e'
            '767fe5c24ba8e4f402821b4262efe890aa28aba62e536e8a06e487b0e0f2d9fa'
            '18772a1cda98cf538d86a3df650eb0b29992140035aac7a2798effaf0876f0e5')
