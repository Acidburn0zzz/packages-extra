# Contributor: Austin ( doorknob60 [at] gmail [dot] com )
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=linux311-broadcom-wl
_pkgname=broadcom-wl
pkgver=5.100.82.112
pkgrel=0.2
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux311')
makedepends=('linux311-headers')
provides=("$_pkgname=$pkgver")
groups=('linux311-extramodules')

[[ $CARCH = x86_64 ]] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
        'modprobe.d'
        'license.patch'
        'linux-recent.patch')
sha1sums=('01aa32f9e85621253a3f15cf4361bb80d41da3e8'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          '11b87e28f527e111a50b3f5b6b998933a826d205'
          'e03eb9f4a85b9c5375dd3ed7f105af5471f19473')
[[ $CARCH = x86_64 ]] && sha1sums[0]='5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'

backup=("etc/modprobe.d/${pkgname}.conf")
install=install

build() {
	_extramodules=extramodules-3.11-MANJARO
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd "${srcdir}"

	patch -p1 -i linux-recent.patch
	patch -p1 -i license.patch

	make -C /usr/lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	_extramodules=extramodules-3.11-MANJARO
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "${startdir}/install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/${pkgname}.conf
}
