# Based on the file created for Arch Linux by:
# György Balló <ballogy@freestart.hu>

# Maintainer: Guinux <nuxgui@gmail.com>

_pkgbase=libindicator
pkgname=libindicator3
pkgver=0.5.0
pkgrel=1
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
depends=('gtk3>=2.91')
options=(!libtool)
source=(http://launchpad.net/$_pkgbase/0.5/$pkgver/+download/$_pkgbase-$pkgver.tar.gz)
sha256sums=('f05f5e329d55d32000606b9d75d1b2b722b682e440cf7e325440811836554622')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$_pkgbase \
              --disable-static --disable-tests
  make
}

package(){
  cd "$srcdir/$_pkgbase-$pkgver"

  make -C libindicator DESTDIR="$pkgdir/" install
  make -C tools DESTDIR="$pkgdir/" install
}
