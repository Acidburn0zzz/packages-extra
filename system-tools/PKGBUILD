# Maintainer: 謝致邦<Yeking@Red54.com>

pkgname=system-tools
pkgver=3.0.0
pkgrel=1
pkgdesc="Cross-platform configuration utilities for GNOME"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gst/"
license=('GPL2')
options=('!libtool')
depends=('liboobs' 'polkit-gnome' 'dconf')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-common')
conflicts=('gnome-system-tools')
install=$pkgname.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/gnome-system-tools/3.0/gnome-system-tools-${pkgver}.tar.bz2
        po.patch)
sha256sums=('905df26c02f00a6c2c18706ba3db7eab764e3df6576289e103504aa39a47ae0b'
            '827c3169ae5060403b3f833b0f9c1d5cefe1d80aa5ade22e2ff7908b1ce1b5ef')

build() {
    cd "${srcdir}/gnome-system-tools-${pkgver}"
    patch -Np1 -i "${srcdir}/po.patch"
    gnome-autogen.sh
    ./configure --prefix=/usr  --sysconfdir=/etc \
        --localstatedir=/var --disable-scrollkeeper --disable-static --disable-nautilus \
        --disable-services --disable-shares --disable-network --disable-schemas-compile LIBS="-lm -lgthread-2.0"
    make
}

package() {
    cd "${srcdir}/gnome-system-tools-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
