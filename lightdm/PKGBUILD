# Based on the file created for Arch Linux by:
# M0Rf30

# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>

pkgname=lightdm
pkgver=1.3.3
pkgrel=2
pkgdesc="A lightweight display manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm"
license=('GPL3' 'LGPL3')
source=("https://launchpad.net/lightdm/1.4/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        lightdm-autologin
        lightdm-plymouth.service
        lightdm.rc
        lightdm.service
        lightdm.tmpfiles
        xsession)
depends=('dbus-glib' 'libxklavier' 'accountsservice')
options=(!libtool)
install=lightdm.install

sha256sums=(7c5fc82ecdbe7ba5d953d85f1e472d25e17123fc6e882b326f21b6197150eca8
            6a8b286d1ffa04150b3cc401f64e6ddec778c7b65f5bfc831031b64345d7e6b2
            6cc4c79d526e340a644f21980c2be36b497859d77ce00e8389007e740ed45984
            0a23632283c7b53b4e99d0acdf8b1e496abfd0cbb121beae28a8bdcfb27cb9cd
            3fbbfb2d972ee499ccf61f83718873509ce2302c1b2efce6ec8ab568b4bd40d6
            5bd4e9e48b25f2397ef4aede596b79036aec6ecb995521d8230f0e1c92292601
            fb38b265420e55b2fd96f003ab00ad2a2d2cd1902984c84239c8ca84eb9b66a9)

optdepends=('lightdm-gtk-greeter: You need this package to test the default greeter'
            'xorg-server-xephyr: run lightdm in test mode' 
	    'lightdm-unity-greeter: unity lightdm greeter')

makedepends=('gobject-introspection' 'pkg-config' 'intltool' 'patch' 'itstool')

backup=(etc/apparmor.d/lightdm-guest-session
	etc/dbus-1/system.d/org.freedesktop.DisplayManager.conf
	etc/lightdm/keys.conf
	etc/lightdm/lightdm.conf
	etc/lightdm/users.conf
	etc/pam.d/lightdm)

build() {
  cd $srcdir/$pkgname-$pkgver
   ./configure --prefix=/usr \
     --sysconfdir=/etc --disable-static --libexecdir=/usr/lib/lightdm \
     --localstatedir=/var --with-greeter-user=lightdm \
     --with-greeter-session=lightdm-gtk-greeter --disable-tests
   make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

# init services file 
  install -D -m755 ../lightdm.rc $pkgdir/etc/rc.d/lightdm
  install -D -m644 ../lightdm.service $pkgdir/usr/lib/systemd/system/lightdm.service
  install -D -m644 ../lightdm-plymouth.service $pkgdir/usr/lib/systemd/system/lightdm-plymouth.service
  install -D -m644 ../lightdm.tmpfiles $pkgdir/usr/lib/tmpfiles.d/lightdm.conf

# some tweaks  
  rm -rf $pkgdir/etc/init
  chmod +x ../xsession 
  install -D -m755 ../xsession $pkgdir/etc/lightdm
  sed -i -e "s|#run-directory=/var/run/lightdm|run-directory=/run/lightdm|g" $pkgdir/etc/lightdm/lightdm.conf
  sed -i -e "s|minimum-uid=500|minimum-uid=1000|g" $pkgdir/etc/lightdm/users.conf  
  sed -i -e "s|/usr/sbin/nologin|/sbin/nologin|g" $pkgdir/etc/lightdm/users.conf
  sed -i -e "s|#session-wrapper=lightdm-session|session-wrapper=/etc/lightdm/xsession|g" $pkgdir/etc/lightdm/lightdm.conf
  sed -i -e "s|#autologin-session=UNIMPLEMENTED|#autologin-session=UNIMPLEMENTED\n#pam-service=lightdm-autologin|g" $pkgdir/etc/lightdm/lightdm.conf
  install -d -m770 $pkgdir/run/lightdm

# Doing Autologin and security fixes
  cp ../lightdm-autologin $pkgdir/etc/pam.d
  echo "-session optional pam_systemd.so" >> $pkgdir/etc/pam.d/lightdm
  echo "session required pam_limits.so" >> $pkgdir/etc/pam.d/lightdm
  echo " " >> $pkgdir/etc/pam.d/lightdm
  echo "# Allow user to automatically unlock the default gnome-keyring" >> $pkgdir/etc/pam.d/lightdm
  echo "auth optional pam_gnome_keyring.so" >> $pkgdir/etc/pam.d/lightdm
  echo "session optional pam_gnome_keyring.so auto_start" >> $pkgdir/etc/pam.d/lightdm
}
