# Maintainer  : Philip Mueller <philm at manjaro dot org>
# Contributor : Maximilien Noal <noal dot maximilien at gmail dot com>
pkgname=xfce4-volumed-pulse
pkgver=0.2.0
pkgrel=2
pkgdesc="A volume keys control daemon for Xfce (using pulseaudio)"
arch=('i686' 'x86_64')
license=('GPL3')
conflicts=('xfce4-volumed')
url="https://launchpad.net/xfce4-volumed-pulse"
depends=('libnotify' 'pulseaudio' 'libkeybinder2')
makedepends=('xfconf')
optdepends=('xfconf: to store/modify the selected sound card'
            'xfce4-notifyd: for OSD notifications')
md5sums=('e70d8a2b2c6e36bf28bda47927e3756e'
         'e339dc982ae9017026400d466eebfaf7')
source=("https://launchpad.net/xfce4-volumed-pulse/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2"
        "start.sh")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m 755 -D "$srcdir/start.sh" "$pkgdir/"usr/bin/xfce4-volumed-pulse-starter
  sed -i -e 's|Exec=xfce4-volumed-pulse|Exec=xfce4-volumed-pulse-starter|g' "$pkgdir/"etc/xdg/autostart/xfce4-volumed-pulse.desktop
}
