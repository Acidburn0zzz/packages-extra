# Based on the file created for Arch Linux by:
# Det <nimetonmaili at gmail dot com>
# Christian Autermann <christian@autermann.org>
# Martin Lee <hellnest.fuah@gmail.com>
# Ricardo Funke
# PirateJonno <j@skurvy.no-ip.org>
# lh <jarryson#gmail.com>
# Cilyan Olowen <gaknar@gmail.com>
# Biginoz <biginoz _at_ free _point_ fr>
# Shaffer <benjamin dot arnoult at yahoo dot fr>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=plymouth
pkgver=0.8.6.1
pkgrel=4
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango' 'gtk3')
options=('!libtool' '!emptydirs')
install=plymouth.install
backup=('etc/plymouth/plymouthd.conf')
source=('manjaro-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_hook"
        'encrypt_hook.patch'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_install"
        'encrypt_install.patch'
        "http://www.freedesktop.org/software/plymouth/releases/plymouth-$pkgver.tar.bz2"
        'plymouthd.conf'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-pid.patch'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lxdm-plymouth.service'
        'plymouth-quit.service')
sha256sums=('1a57879fa142e83853f92362668fab00e7959d3dadd185eac9dec230661199d3'
            'e0cbcabb81233b4d465833dca0faf1e762dc3cb6611597a25fe24e5d7209f316'
            'e5e85523098f948b1feb6bcf0595d2b2cbb6665c11e394cfe312533f352263b8'
            'cfe465bdad3d958bb2332a05e04f2e1e884422a5714dfd1a0a3b9b74bf7dc6ae'
            'eec0f625ad1981ac6516afb622daad266e25b22671c72eadefa1df3a0cc4bb8d'
            '87f9126bb80457c3c9966df765115ea2ebb10b42660be37ede73350bc0f01d1d'
            '83ffd4aa52ca6a77423ab7edc2c2369808c7abffe68d97e134176c4592a81cc6'
            'f9caa93e784ed1f045c6c12d39c0f89f5391ecfa8e12ba3f021a0a4d55234815'
            'c1d44564bad8c4d49ef7eb5083f0339f11ef2b3215ee8a5e6c56a586a8d31fac'
            'dbde9ea5659291a010874a9be436c01926e8a70a03f799ec6ac7166db290c6fd'
            '20dc6462c89f721c4ec91fc407f62db836b83edef091630caf29c1a1ae81393b'
            '1292cc3ac2b73020f2a94a0ec53b238ec7c7321b66e59d243d6b645f794499fb'
            'f965d311bd66e426723fae20161a152af59adc1831549d92bef874d868c50f31'
            'cb69f873cf95b294428c7c3dcdb36cbf38d738d8ee43cec7e7e84e5254c4eead'
            'e82ba28fb582f7dca54f4647fa62db627dc8f834de77e5389d70b4d0e4f5a33e'
            '41d20d510eeec8aa1ee7a2cc6eb6134a2535bc47d2ade465b55dc06f1a3d9640'
            '3040f4e3055b6c0b68777fb33061ecd2c781162e09fdeed15da8bd4fcc494520'
            '2fd66867c829e51308f460eda581aec4be42d5b1ae57a537baf6d594f9007f25')

build() {
  msg "Applying Patches..."

  patch -p0 -i encrypt_hook.patch
  patch -p0 -i encrypt_install.patch

  cd $pkgname-$pkgver
  msg2 "Setting a human-readable error for '# plymouth-set-default-theme -R'"
  patch -p0 -i ../plymouth-set-default-theme.in.patch

  msg2 "Fixing updating initrd"
  patch -p0 -i ../plymouth-update-initrd.patch

  msg2 "Changing PID file to /run/plymouth.pid"
  patch -p0 -i ../plymouth-pid.patch

  msg2 "Changing SYSTEMD_UNIT_DIR to /usr/lib/systemd/system/"
  # edit systemd-unit-dir
  sed -e 's:SYSTEMD_UNIT_DIR=/lib/systemd/system:SYSTEMD_UNIT_DIR=/usr/lib/systemd/system:g' \
       -i configure.ac

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  # aclocal
  sed -e 's:^.*am__api_version=.*:am__api_version=1.12:' \
       -i configure

  msg "Starting make..."

  ./configure --prefix=/usr --exec-prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --enable-tracing \
    --without-rhgb-compat-link \
    --with-gdm-autostart-file=yes \
    --enable-gdm-transition \
    --enable-systemd-integration \
    --with-gdm-autostart-file=yes \
    --with-release-file=/etc/manjaro-release \
    --with-logo=/usr/share/plymouth/manjaro-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D \
    --without-system-root-install

  make
}

package() {
  cd "${srcdir}/plymouth-$pkgver"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/plymouth.initcpio_install" "${pkgdir}/usr/lib/initcpio/install/plymouth"
  install -Dm644 "${srcdir}/plymouth.initcpio_hook" "${pkgdir}/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "${srcdir}/encrypt_install" "${pkgdir}/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "${srcdir}/encrypt_hook" "${pkgdir}/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "${srcdir}/plymouth.functions" "${pkgdir}/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "${srcdir}/manjaro-logo.png" "${pkgdir}/usr/share/plymouth/manjaro-logo.png"
  install -Dm644 "${srcdir}/plymouthd.conf" "${pkgdir}/etc/plymouth/plymouthd.conf"

  # Plymouth<->systemd integration, including units for DMs
  for i in {{gdm,kdm{,-unpatched},lxdm}-plymouth,plymouth-quit}.service; do
    install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
  done
}
