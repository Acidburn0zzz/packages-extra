# Based on the file created for Arch Linux by:
# Det <nimetonmaili at gmail dot com>
# Christian Autermann <christian@autermann.org>
# Martin Lee <hellnest.fuah@gmail.com>
# Ricardo Funke
# PirateJonno <j@skurvy.no-ip.org>
# lh <jarryson#gmail.com>
# Cilyan Olowen <gaknar@gmail.com>
# Biginoz <biginoz _at_ free _point_ fr>
# Shaffer <benjamin dot arnoult at yahoo dot fr>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=plymouth
pkgver=0.8.5.1
pkgrel=3
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango' 'gtk3')
options=('!libtool' '!emptydirs')
install=plymouth.install
backup=('etc/plymouth/plymouthd.conf')
source=('manjaro-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-${CARCH}/encrypt_hook"
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-${CARCH}/encrypt_install"
        "http://www.freedesktop.org/software/plymouth/releases/plymouth-$pkgver.tar.bz2"
        'plymouthd.conf'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-update-initrd.patch')
sha256sums=('1a57879fa142e83853f92362668fab00e7959d3dadd185eac9dec230661199d3'
            'e0cbcabb81233b4d465833dca0faf1e762dc3cb6611597a25fe24e5d7209f316'
            'cfe465bdad3d958bb2332a05e04f2e1e884422a5714dfd1a0a3b9b74bf7dc6ae'
            '5bf94a53c090e646436e6750e8e228fcd166bc04e34a7fd62189bdf9e450937c'
            '83ffd4aa52ca6a77423ab7edc2c2369808c7abffe68d97e134176c4592a81cc6'
            '19c14656385444f51ce5ad2f3b768b0c0e9b090ccd62841677b63090ebc05d7f'
            'bc8c2485891974717c6941aae586009d26421e0cca96a7d3eca47838692ef6e8'
            'c2a69f4243e52d026d58ced86caa17e476054d29f7ce766d6f13ed251e69a817'
            'f965d311bd66e426723fae20161a152af59adc1831549d92bef874d868c50f31')

build() {
  msg "Applying Patches..."
  cd "${srcdir}/plymouth-$pkgver"
  patch -p0 -i "${srcdir}/plymouth-update-initrd.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  # edit systemd-unit-dir
  sed -e 's:SYSTEMD_UNIT_DIR=/lib/systemd/system:SYSTEMD_UNIT_DIR=/usr/lib/systemd/system:g' \
       -i configure.ac

  # force /usr/lib
  sed -e 's:$(subst /usr/lib,/lib,$(libdir)):$(subst /usr/lib,/usr/lib,$(libdir)):g' \
       -i src/libply/Makefile.in

  sed -e 's|$(libdir:/usr%=%)|$(libdir)|g' \
       -i src/libply-splash-core/Makefile.in

  msg "Starting make..."

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --enable-tracing \
    --with-system-root-install \
    --enable-gdm-transition \
    --enable-systemd-integration \
    --with-gdm-autostart-file=yes \
    --with-release-file=/etc/manjaro-release \
    --with-logo=/usr/share/plymouth/manjaro-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D

  make
}

package() {
  cd "${srcdir}/plymouth-$pkgver"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/plymouth.initcpio_install" "${pkgdir}/usr/lib/initcpio/install/plymouth"
  install -Dm644 "${srcdir}/plymouth.initcpio_hook" "${pkgdir}/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "${srcdir}/encrypt_install" "${pkgdir}/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "${srcdir}/encrypt_hook" "${pkgdir}/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "${srcdir}/plymouth.functions" "${pkgdir}/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "${srcdir}/manjaro-logo.png" "${pkgdir}/usr/share/plymouth/manjaro-logo.png"
  install -Dm644 "${srcdir}/plymouthd.conf" "${pkgdir}/etc/plymouth/plymouthd.conf"
}
