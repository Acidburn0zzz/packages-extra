#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=live-installer
pkgname=('live-installer' 'manjaro-faces')
pkgver=0.8.8pre0
_ver=0.8.8
pkgrel=2
pkgdesc="Manjaro Live-Installer"
arch=(any)
url="https://git.manjaro.org/core/live-installer"
license=('GPL')
makedepends=('gettext')
options=(!emptydirs)
install=
source=("live-installer-$_ver.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd "$srcdir/$pkgbase-$_ver"
	# create .mo translation files
	mkdir -p mo
	for i in `ls po | sed s'|.po||'` ; do
		msgfmt -c "po/$i.po" -o "mo/$i.mo"
	done
}

package_live-installer() {
  depends=('python2' 'pygtk' 'pyparted-git' 'pywebkitgtk' 'gparted' 'inxi' 'python2-geoip' 'python2-pyqt' 'python2-imaging' 'python2-numpy' 'imagemagick' 'keyboardctl' 'sudo' 'pacman' 'manjaro-faces')
  optdepends=('opencv: webcam support')
  backup=(etc/live-installer/install.conf)
	cd "$srcdir/$pkgbase-$_ver"
	cp -rv "usr" "${pkgdir}"
	cp -rv "etc" "${pkgdir}"
	# rename mo files
	for i in `ls mo | sed s'|.mo||'` ; do
		mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
		mv "mo/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/live-installer.mo"
	done
}

package_manjaro-faces() {
  pkgdesc="Manjaro Live-Installer Slideshow"
  conflicts=("live-installer-fav")
  replaces=("live-installer-fav")
	cd "$srcdir/$pkgbase-$_ver"
  mkdir -p "${pkgdir}"/usr/share/pixmaps/faces/
	cp -rv "fav/." "${pkgdir}"/usr/share/pixmaps/faces/
}

# vim:set ts=2 sw=2 et:
