# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pyalpm
pkgver=0.6.2.1
pkgrel=1
pkgdesc="Libalpm bindings for Python 3"
arch=('i686' 'x86_64')
url="http://projects.archlinux.org/users/remy/pyalpm.git/"
license=('GPL')
depends=('python>=3.2' 'pacman>=4.1')
makedepends=('git')
source=("ftp://ftp.archlinux.org/other/pyalpm/$pkgname-0.6.2.tar.gz"
        'custom.patch')
sha256sums=('42ac78baf8221c04fc9a0cb29dd8e4fb425d054292fd9206a2eb7fc21e0564a0'
            'cfc5d1f5ec7dc5010264a7294e3d51b46a77915c8a38ad595bf6d9e59fc5eb10')

build() {
  cd ${srcdir}/${pkgname}-0.6.2

  # apply custom patch
  patch -Np1 -i "${srcdir}/custom.patch"
  
  # workaround Python packaging issue (FS#34658)
  export CFLAGS="$CFLAGS -Wno-format"

  python setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-0.6.2
  python setup.py install --root=${pkgdir}
}
