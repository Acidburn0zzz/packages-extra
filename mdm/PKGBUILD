# Maintainer:  Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer:  Roland Singer <roland[at]manjaro[dog]org>
# Contributors: twa022 <twa022 at gmail dot com>
# Contributors: Pablo Lezaeta <prflr88 (arroba) gmail (dot) com>

pkgname=mdm
pkgver=1.3.1
pkgrel=2
pkgdesc="Linux Mint display manager, a fork of GDM 2.20.x"
arch=('i686' 'x86_64')
url="http://www.linuxmint.com"
license=('GPL')
groups=()
depends=('pam>=1.0.2' 'libdmx' 'gtk2>=2.6' 'libgnomecanvas>=1.109.0' 'librsvg>=1.1.1' 'libxml2>=2.4.12' 'libart-lgpl>=2-0'
'dbus-glib>=0.80' 'libwebkit' 'xdg-su')
makedepends=('gnome-common' 'intltool>=0.35.0' 'perl>=5.8.1' 'gnome-doc-utils>=0.3.2' 'xorg-server' 'pango' 'zenity' 'popt')
optdepends=('webkitgtk: mdm-theme-emulator' 'xorg-server-xnest: mdmthemetester')
provides=('gdm' 'gdm-old' "mintdm=$pkgver")
conflicts=('gdm' 'mintdm')
replaces=()
backup=()
options=('!libtool')
install='mdm.install'
changelog=
source=("$pkgname-$pkgver.zip::https://github.com/linuxmint/$pkgname/archive/$pkgver.zip"
	'mdm.pam'
	'mdm.service'
	'mdm-autologin.pam'
	'defaults.conf'
	'mdm-plymouth.service')
sha256sums=(6de10a12f8df88e1e9fba905df636eaf9c18fb020e3588f1591ce572d7234ce0
            54c3ff63cf18cafb5d6fead8cbe07f278a2ed41b6f2b354b6e28845bc850d60d
            d3dd582eb25bb31f012167069c869d3ce89c1dbd9b5aa7396350d8cf609994a5
            3db920fb26b10a22dd8c4ebaf1de1d1e83de613be61d8bd5206b8eb9ffedc2d7
            01cac1714eb82bfda9260e83e5e9728eda3966c7653ce2ead9d38a9ccda26855
            7579ed402ee0db60e0c40ddbb7b2c80be0aec4315e45aa525908bef8aa181bd6)

noextract=()

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	chmod +x ./autogen.sh
	export AUTOMAKE=automake
	./autogen.sh --enable-ipv6=yes --with-prefetch \
	--prefix=/usr --with-console-kit=no --sysconfdir=/etc \
	--with-systemd --libexecdir=/usr/lib/mdm --localstatedir=/var/lib \
	--disable-static --with-xevie=yes --disable-scrollkeeper \
	--enable-secureremote=yes --enable-ipv6=yes \
    --enable-compile-warnings=no --sbindir=/usr/bin \
	LDFLAGS="-lXau -lm"
	sed -i -e 's|${prefix}|/usr|' config.h
	make DESTDIR=$pkgdir
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=$pkgdir install

msg2 'Adding PAM rules'
	#PAM, we use our own, not LinuxMint stuff, problem?...
	install -m755 -d $pkgdir/etc/pam.d
	install -m644 $srcdir/mdm-autologin.pam $pkgdir/etc/pam.d/mdm-autologin
	install -m644 $srcdir/mdm.pam $pkgdir/etc/pam.d/mdm

msg2 'Adding SystemD services'
	#systemd Init script, but ... SysV Inint is soo old
	install -m755 -d $pkgdir/usr/lib/systemd/system
	install -m644 $srcdir/mdm.service $pkgdir/usr/lib/systemd/system/mdm.service

msg2 'Adding config files'
	#configuration
	install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/mdm/" 
	rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop || true

	#Why on the hay this directory is created empty in etc, remove it 
	rmdir $pkgdir/etc/dm

msg2 'Adding faces'
	#Fix mdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
	install -m755 -d $pkgdir/usr/share/pixmaps/faces/

	make DESTDIR=$pkgdir install -C gui/faces

msg2 'Adding aplications'
	#Fix gdmsetup.desktop
	sed -i -e 's|^Exec=gksu|Exec=xdg-su|' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop
	sed -i -e 's|^NoDisplay=true|NoDisplay=false |' $pkgdir/usr/share/mdm/applications/mdmphotosetup.desktop

	#Fix erroneous path for certain aplications
	install -m755 -d $pkgdir/usr/share/applications
	mv -f $pkgdir/usr/share/mdm/applications/*.* $pkgdir/usr/share/applications
	chmod 755 $pkgdir/usr/share/applications/*.*
	rmdir $pkgdir/usr/share/mdm/applications

}
