# Maintainer: Philip MÃ¼ller <philm_at_manjaro_dot_org>
# Contributor: Robert Gormly <rggormly_at_gmail_dot_com>

pkgname=xfce-theme-greenbird
pkgver=20131109
pkgrel=1
pkgdesc="Desktop suite for Xfce"
arch=('any')
url="http://git.manjaro.org/artwork/greenbird-theme"
license=('GPL2' 'CCPL')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('faenza-green-icon-theme: matching icon theme'
            'lightdm-gtk-greeter: lightdm theme'
            'lightdm-unity-greeter: lightdm unity theme')

source=("$pkgname-$pkgver.tar.gz::http://git.manjaro.org/artwork/greenbird-theme/repository/archive?ref=$pkgver")
sha256sums=('571a3a0ceb2c888f3edb31d2ce0cac176726cb1888afbe6831e569cee660a6fb')

package() {
  cd "$srcdir/greenbird-theme.git"

  # create theme dirs
  install -d -m 755 "$pkgdir"/usr/share/themes/Greenbird{,-compact/xfwm4}

  # install compact theme
  install -m 644 xfwm4_compact/* "$pkgdir"/usr/share/themes/Greenbird-compact/xfwm4/

  # clean up
  rm -rf {.git,.gitignore,LICENSE.{CC,GPL},README,xfwm4_compact}

  # install theme
  cp -r . "$pkgdir"/usr/share/themes/Greenbird/
}

# vim:set ts=2 sw=2 et:
