# Maintainer: Philip MÃ¼ller <philm_at_manjaro_dot_org>
# Contributor: Robert Gormly <rggormly_at_gmail_dot_com>

pkgname=xfce-theme-greenbird
pkgver=20121213
pkgrel=1
pkgdesc="Desktop suite for Xfce"
arch=('any')
url="git://git.manjaro.org/manjaro-linux-graphics/greenbird.git"
license=('GPL2' 'CCPL')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('faenza-green-icon-theme: matching icon theme'
            'lightdm-gtk-greeter: lightdm theme'
            'lightdm-unity-greeter: lightdm unity theme')

_gitroot="git://git.manjaro.org/manjaro-linux-graphics/greenbird.git"
_gitname="Greenbird"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"

  # create theme dirs
  install -d -m 755 "$pkgdir"/usr/share/themes/Greenbird{,-compact/xfwm4}

  # install compact theme
  install -m 644 xfwm4_compact/* "$pkgdir"/usr/share/themes/Greenbird-compact/xfwm4/

  # clean up
  rm -rf {.git,.gitignore,LICENSE.{CC,GPL},README,xfwm4_compact}

  # install theme
  cp -r . "$pkgdir"/usr/share/themes/Greenbird/
}

# vim:set ts=2 sw=2 et:
