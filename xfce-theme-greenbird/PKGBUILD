# Maintainer: Philip MÃ¼ller <philm_at_manjaro_dot_org>
# Contributor: Robert Gormly <rggormly_at_gmail_dot_com>

pkgname=xfce-theme-greenbird
pkgver=20140421
pkgrel=2
_git=51efd00d04e5947ed75b736add58c333241e578a
pkgdesc="Desktop suite for Xfce"
arch=('any')
url="http://git.manjaro.org/artwork/greenbird-theme"
license=('GPL2' 'CCPL')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('faenza-green-icon-theme: matching icon theme'
            'lightdm-gtk-greeter: lightdm theme'
            'lightdm-unity-greeter: lightdm unity theme')

source=("$pkgname-$pkgver.tar.gz::https://github.com/manjaro/artwork-greenbird/archive/$_git.tar.gz")
sha256sums=('c2c9992e5a6c0349bd081058a3f51ff30bb0db725e01f059729ed7a7a5a77767')

package() {
  cd "$srcdir/artwork-greenbird-$_git"

  # create theme dirs
  install -d -m 755 "$pkgdir"/usr/share/themes/Greenbird{,-compact/xfwm4}

  # install compact theme
  install -m 644 xfwm4_compact/* "$pkgdir"/usr/share/themes/Greenbird-compact/xfwm4/

  # clean up
  rm -rf {.git,.gitignore,LICENSE.{CC,GPL},README,xfwm4_compact}

  # install theme
  cp -r . "$pkgdir"/usr/share/themes/Greenbird/
}

# vim:set ts=2 sw=2 et:
