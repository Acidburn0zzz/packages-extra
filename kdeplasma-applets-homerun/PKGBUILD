# Maintainer: birdflesh <antkoul at gmail dot com>

pkgname=kdeplasma-applets-homerun
_pkgname=homerun
pkgver=0.2.1
pkgrel=1
pkgdesc="An application launcher for KDE Plasma desktop"
arch=('i686' 'x86_64')
url="http://userbase.kde.org/Homerun"
license=('GPL' 'LGPL' 'BSD')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
conflicts=('kdeplasma-applets-homerun-git')
install=$pkgname.install
source=("http://download.kde.org/unstable/$_pkgname/src/$_pkgname-$pkgver.tar.bz2")
md5sums=('8987d9b2f5ff82ba89495c705f75488a')

build() {
  cd "$srcdir"
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$_pkgname-$pkgver/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
