# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=imagewriter
pkgver=1.10
_opensuse_rel=3.1
pkgrel=2
pkgdesc="SUSE Studio Imagewriter - Graphical image writer application"
url="http://kiwi.berlios.de"
arch=('x86_64' 'i686')
license=('GPLv2')
depends=('qt4' 'xdg-su')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/imagewriter-${pkgver}-${_opensuse_rel}.src.rpm"
        'build_fix.patch')
sha512sums=('689c6fae74e39f7c0fe15747aaf8ebe5d4bc36739d7fbaa7d4c4448c6011771f3fb26c6dcaf1f98dc7aa17cd7ee8b3bc6b820ae7ad5a328ac9d435f02f0c96f2'
 '42034f630d0cd097a94e16ea6428c558ec00f16051330cb1bbaf0a2c5c496a6ece997328fba5ffed79cf13aa13299e787c1b82e4a29a7b92438bc63b36d278e0')

build() {
  cd "${srcdir}"
  rm -vf imagewriter.spec
  tar zxvf imagewriter-${pkgver}.tar.gz
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/build_fix.patch"
  qmake-qt4 PREFIX=/usr imagewriter.pro
  make CFLAGS="${CFLAGS} -DKIOSKHACK -Wno-unused-parameter"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
  # fix desktop file
  sed -i 's|/usr/bin/xdg-su -c|/usr/bin/xdg-su root|g' "${pkgdir}"/usr/share/applications/imagewriter.desktop
}

# vim:set ts=2 sw=2 et:
