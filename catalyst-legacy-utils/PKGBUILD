# Partly based on original PKGBUILD by Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Contributor: csslayer
# Contributor: cvigano

# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgbase=catalyst-legacy-utils
pkgname=('catalyst-legacy-utils' 'opencl-catalyst-legacy' 'mhwd-catalyst-legacy')
pkgver=12.6
pkgrel=4
pkgdesc="AMD/ATI catalyst-legacy drivers utilities and libraries with OpenCL implementation"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')

source=(
    http://www2.ati.com/drivers/legacy/${pkgver/./-}/amd-driver-installer-${pkgver}-legacy-x86.x86_64.zip
    catalyst.sh
    amdcccle.desktop
    atieventsd.sh)

md5sums=('eada88ea8ec12e09f516527b5a5d55d6'
         'bdafe749e046bfddee2d1c5e90eabd83'
         '4efa8414a8fe9eeb50da38b5522ef81d'
         'f729bf913613f49b0b9759c246058a87')
build() {
  ## Unpack archive
    /bin/sh ./amd-driver-installer-${pkgver}-legacy-x86.x86_64.run --extract archive_files
}

package_catalyst-legacy-utils() {
  depends=('catalyst-input' 'catalyst-video' 'catalyst-server>=1.12.0' 'catalyst-server<1.13.0' 'netkit-bsd-finger' 'libxrandr' 'libsm' 'fontconfig' 'libxcursor' 'libxi' 'gcc-libs' 'libxinerama' 'mhwd' 'libgl')
  optdepends=('qt4: to run ATi catalyst Control Center (amdcccle)'
	      'libxxf86vm: to run ATi catalyst Control Center (amdcccle)'
	      'opencl-headers: headers necessary for OpenCL development')
  conflicts=('catalyst' 'catalyst-utils')
  provides=("libatical=${pkgver}")
  install=${pkgname}.install
  ## Install userspace tools and libraries
    # Create directories
      install -m755 -d "${pkgdir}/etc/ati"
      install -m755 -d "${pkgdir}/etc/rc.d"
      install -m755 -d "${pkgdir}/etc/profile.d"
      install -m755 -d "${pkgdir}/etc/acpi/events"
      install -m755 -d "${pkgdir}/etc/security/console.apps"
      install -m755 -d "${pkgdir}/etc/OpenCL/vendors"  # since 11.11

      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/drivers"
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions/fglrx" # since 11.4
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/linux"
      install -m755 -d "${pkgdir}/usr/lib/dri"
      install -m755 -d "${pkgdir}/usr/lib/fglrx" # since 11.4

      install -m755 -d "${pkgdir}/usr/bin"
      install -m755 -d "${pkgdir}/usr/sbin"

      install -m755 -d "${pkgdir}/usr/include/X11/extensions"
      install -m755 -d "${pkgdir}/usr/include/GL"

      install -m755 -d "${pkgdir}/usr/share/applications"
      install -m755 -d "${pkgdir}/usr/share/ati/amdcccle"
      install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
      install -m755 -d "${pkgdir}/usr/share/man/man8"
      install -m755 -d "${pkgdir}/usr/share/pixmaps"

    # X.org driver
      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/xpic/usr/X11R6/lib/modules"
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/xpic_64a/usr/X11R6/lib64/modules"
      fi

     # *.a added in 11.2, and removed in 11.3...
      #install -m644 *.a "${pkgdir}/usr/lib/xorg/modules/"
      install -m755 *.so "${pkgdir}/usr/lib/xorg/modules/"
      install -m755 drivers/*.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
      install -m755 linux/*.so "${pkgdir}/usr/lib/xorg/modules/linux/"
      #install -m755 extensions/libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/" #before 11.4
      install -m755 extensions/fglrx/fglrx-libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/fglrx/fglrx-libglx.so" # since 11.5
      ln -snf /usr/lib/xorg/modules/extensions/fglrx/fglrx-libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so.catalyst" # since 12.6
      #install -m755 extensions/libdri.so "${pkgdir}/usr/lib/xorg/modules/extensions/libdri.ati"

    # Controlcenter / libraries
      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/arch/x86/usr"
	_lib=lib
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/arch/x86_64/usr"
	_lib=lib64
      fi

      install -m755 X11R6/bin/* "${pkgdir}/usr/bin/"
      install -m755 sbin/* "${pkgdir}/usr/sbin/"
      #install -m755 X11R6/${_lib}/*.so* "${pkgdir}/usr/lib/" || return #before 11.4
      install -m755 X11R6/${_lib}/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/fglrx" # since 11.5
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/fglrx/libGL.so.1.2" # since 11.4
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/fglrx-libGL.so.1.2" # since 11.4
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so.catalyst" # since 12.6
      install -m755 X11R6/${_lib}/libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/" # since 11.4
      ln -snf libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/libAMDXvBA.so.1" # since 11.4
      ln -snf libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/libAMDXvBA.so" # since 11.4
      install -m755 X11R6/${_lib}/libatiadlxx.so "${pkgdir}/usr/lib/" # since 11.4
      install -m755 X11R6/${_lib}/libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/" # since 11.4
      install -m755 X11R6/${_lib}/libXvBAW.so.1.0 "${pkgdir}/usr/lib/" # since 11.4
      ln -snf libXvBAW.so.1.0 "${pkgdir}/usr/lib/libXvBAW.so.1" # since 11.4
      ln -snf libXvBAW.so.1.0 "${pkgdir}/usr/lib/libXvBAW.so" # since 11.4
      install -m644 X11R6/${_lib}/*.a "${pkgdir}/usr/lib/" # really needed?
      install -m644 X11R6/${_lib}/*.cap "${pkgdir}/usr/lib/"
      install -m755 X11R6/${_lib}/modules/dri/*.so "${pkgdir}/usr/lib/xorg/modules/dri/"
      install -m755 ${_lib}/*.so* "${pkgdir}/usr/lib/"

    ## QT libs (only 2 files) - un-comment 2 lines below if you don't want to install qt package
#      install -m755 -d "${pkgdir}/usr/share/ati/${_lib}"
#      install -m755 share/ati/${_lib}/*.so* "${pkgdir}/usr/share/ati/${_lib}/"

      ln -snf /usr/lib/xorg/modules/dri/fglrx_dri.so ${pkgdir}/usr/lib/dri/fglrx_dri.so
      ln -snf libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/libfglrx_dm.so.1"
      ln -snf libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/libfglrx_dm.so"
      ln -snf libatiuki.so.1.0 "${pkgdir}/usr/lib/libatiuki.so.1"
      ln -snf libatiuki.so.1.0 "${pkgdir}/usr/lib/libatiuki.so"


      cd "${srcdir}"/archive_files/common
      install -m644 etc/ati/* "${pkgdir}/etc/ati/"
      chmod 755 "${pkgdir}/etc/ati/authatieventsd.sh"

      #security provided with 10.9, is it working fine?
      install -m644 etc/security/console.apps/amdcccle-su "${pkgdir}/etc/security/console.apps/"

     # *.h removed in 11.3...
      #install -m644 usr/X11R6/include/X11/extensions/*.h "${pkgdir}/usr/include/X11/extensions/"
      install -m755 usr/X11R6/bin/amdupdaterandrconfig "${pkgdir}/usr/bin/"
      install -m644 usr/include/GL/*.h "${pkgdir}/usr/include/GL/"
      install -m755 usr/sbin/*.sh "${pkgdir}/usr/sbin/"
      install -m644 usr/share/ati/amdcccle/* "${pkgdir}/usr/share/ati/amdcccle/"
      install -m644 usr/share/icons/*.xpm "${pkgdir}/usr/share/pixmaps/"
      install -m644 usr/share/man/man8/*.8 "${pkgdir}/usr/share/man/man8/"
      install -m644 "${srcdir}/amdcccle.desktop" "${pkgdir}/usr/share/applications/"

    # ACPI example files
      install -m755 usr/share/doc/fglrx/examples/etc/acpi/*.sh "${pkgdir}/etc/acpi/"
      sed -i -e 's/usr\/X11R6/usr/g' "${pkgdir}/etc/acpi/ati-powermode.sh"
      install -m644 usr/share/doc/fglrx/examples/etc/acpi/events/* "${pkgdir}/etc/acpi/events/"

    # Add ATI Events Daemon launcher
      install -m755 "${srcdir}/atieventsd.sh" "${pkgdir}/etc/rc.d/atieventsd"

    # thanks to cerebral, we dont need that damned symlink
      install -m755 "${srcdir}/catalyst.sh" "${pkgdir}/etc/profile.d/"

    # provided in opencl-catalyst-legacy package
      rm "${pkgdir}"/usr/lib/lib{amdocl*,OpenCL}.so* 

    # License
      install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_opencl-catalyst-legacy() {
  pkgdesc="OpenCL implemention from AMD"
  provides=('libcl')
  conflicts=('libcl')
  depends=('gcc-libs')
  optdepends=('opencl-headers: headers necessary for OpenCL development')

  install -dm755 "${pkgdir}"/usr/{bin,lib}
  install -dm755 "${pkgdir}/etc/OpenCL/vendors"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  if [ "${CARCH}" = "i686" ]; then
    _ARCHDIR="x86"
    _ARCHLIB=""
  else
    _ARCHDIR="x86_64"
    _ARCHLIB="64"
  fi

  cd ${srcdir}/archive_files/arch/${_ARCHDIR}
  
  install -m755 usr/bin/clinfo "${pkgdir}/usr/bin/"
  install -m644 etc/OpenCL/vendors/amdocl*.icd "${pkgdir}/etc/OpenCL/vendors/"
  install -m755 usr/lib${_ARCHLIB}/libamdocl*.so "${pkgdir}/usr/lib/"
  install -m755 usr/lib${_ARCHLIB}/libOpenCL.so.1 "${pkgdir}/usr/lib/"
  ln -s libOpenCL.so.1 "${pkgdir}/usr/lib/libOpenCL.so"
  
  # license
  install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mhwd-catalyst-legacy() {
    pkgdesc="MHWD module-ids for catalyst-legacy $pkgver"
    arch=('any')
    install -d -m755 "${pkgdir}/var/lib/mhwd/ids/pci/"
    # Generate mhwd database
    sh -e ${srcdir}/../mhwd-catalyst \
    ${srcdir}/archive_files/common/lib/modules/fglrx/build_mod/fglrxko_pci_ids.h \
    > ${pkgdir}/var/lib/mhwd/ids/pci/catalyst-legacy.ids
}
