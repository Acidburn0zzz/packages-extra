# Based on the file created for Arch Linux by:
# M0Rf30

# Maintainer: Guinux <nuxgui@gmail.com>
# Contributor: Alex Filgueira

pkgname=cinnamon
pkgver=1.6.7
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL')
depends=('accountsservice' 'caribou' 'python2-dbus' 'dbus-glib' 'folks' 'gjs' 'gnome-bluetooth' 'gnome-desktop' 'gnome-themes-standard' 'gnome-menus' 
'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'libcroco' 'libgnomekbd' 'libpulse' 'muffin' 'networkmanager' 
'notification-daemon' 'python2' 'python2-gconf' 'telepathy-logger' 'gnome-menus2' 'gnome-control-center' 'python2-lxml' 'python-imaging')
makedepends=('gnome-common' 'intltool')
optdepends=('nemo-fm: the official Cinnamon file manager (a Nautilus fork)'
			'nautilus: the official Gnome file manager')
conflicts=('cinnamon-git')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/$pkgver"
        cinnamon-screensaver.desktop
        cinnamon2d-screensaver.desktop)
sha256sums=('283d8ec894831df5c062e88196c5f0191daa99b1da1cbfada1547a700f50ab24'
            '1c21bf237618d68a2f5f834f6602ca002bef1d69daa2fc6af74fc7ef9bfc96ac'
            'ce9172af04758a4078079e6bcb7b1117bfce02e3a9a7f794f32d7359612a3aa0')

build() {
  LDFLAGS="/usr/lib/libgthread-2.0.so"
  CFLAGS="${CFLAGS} -Wno-deprecated-declarations"
  cd ${srcdir}/linuxmint-Cinnamon*
 
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon  \
               --localstatedir=/var --disable-static --disable-schemas-compile \
               --disable-schemas-install --enable-compile-warnings=yes --with-session-tracking=systemd
 
  make GLIB_COMPILE_SCHEMAS=glib-compile-schemas
}

package() {
  cd ${srcdir}/linuxmint-Cinnamon*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  
  # Python2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/$pkgname-extension-tool" \
    "${pkgdir}/usr/lib/$pkgname-settings/$pkgname-settings.py" \
    "${pkgdir}/usr/bin/$pkgname-settings" \
    "${pkgdir}/usr/bin/$pkgname-menu-editor"
    
  # Kdm fix
  mkdir -p  $pkgdir/usr/share/apps/kdm/sessions/
  cp $pkgdir/usr/share/xsessions/*.desktop $pkgdir/usr/share/apps/kdm/sessions/

  # Screensaver gnome 3.6 fix
  mkdir -p $pkgdir/etc/xdg/autostart
  cp $srcdir/cinnamon*-screensaver.desktop $pkgdir/etc/xdg/autostart
}
