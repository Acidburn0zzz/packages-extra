# Maintainer: jjacky
pkgname=kalu
pkgver=2.2.0
pkgrel=1
pkgdesc="Upgrade notifier w/ AUR support, watched (AUR) packages, news"
arch=('i686' 'x86_64')
url="https://github.com/jjk-jacky/kalu"
license=('GPL3+')
depends=('dbus' 'polkit' 'gtk3' 'pacman>=4.1.1' 'pacman<4.2' 'curl' 'libnotify' 'notification-daemon')
makedepends=('perl' 'groff' 'manjaro-artwork')
source=(http://jjacky.com/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('53fa2b1a51b147d96719a5051820639183fa36992579c079f0fae668f67e184f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # manjarofy
  sed -i 's|Arch|Manjaro|g' {AUTHORS,README*,configure*,Makefile*}
  sed -i 's|Arch|Manjaro|g' {build-aux,misc,doc,po,m4}/*
  sed -i 's|Arch|Manjaro|g' */kalu*/*
  sed -i 's|misc/arch_linux_48x48_icon_by_painlessrob.png|misc/manjaro48.png|g' Makefile*
  sed -i 's|Icon by Painless Rob|Icon by Roland Singer|g' {build-aux,misc,doc,po,m4}/*
  sed -i 's|https://bbs.archlinux.org/viewtopic.php?id=130839|http://git.manjaro.org/manjaro-linux-graphics/manjaro-logo|g' AUTHORS
  sed -i 's|https://bbs.archlinux.org/viewtopic.php?id=130839|http://git.manjaro.org/manjaro-linux-graphics/manjaro-logo|g' {build-aux,misc,doc,po,m4}/*
  sed -i 's|https://www.archlinux.org/feeds/news/|http://manjaro.org/feed/|g' configure*
  sed -i 's|http://www.archlinux.org|http://www.manjaro.org|g' {build-aux,misc,doc,po,m4}/*

  cp /usr/share/icons/logo5.png $srcdir/$pkgname-$pkgver/misc/manjaro48.png
  ./configure --prefix=/usr --with-news-rss-url=http://blog.manjaro.org/feed/
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}

# vim:set ts=2 sw=2 et:
